ledger = _{ SOI ~ (include | option | unit | statement | comment | whitespace | newline)* ~ EOI }

include = { "include" ~ whitespace+ ~ string }
option =  { "option"  ~ whitespace+ ~ string ~ whitespace* ~ string }
unit =    { "unit"    ~ whitespace+ ~ currency }

comment = _{ whitespace* ~ ";" ~ (!newline ~ ANY)* }

statement = { date ~ whitespace+ ~
    (custom_statement
    | open_statement
    | close_statement
    | price_statement
    | pad_statement
    | balance_statement
    | transaction)
}
    custom_statement =  { "custom" ~ (whitespace+ ~ string)+ }
    open_statement =    { "open"   ~ whitespace+ ~ account }
    close_statement =   { "close"  ~ whitespace+ ~ account }
    price_statement =   { "price"  ~ whitespace+ ~ currency ~ whitespace+ ~ amount }
    pad_statement =     { "pad"    ~ whitespace+ ~ account ~ whitespace+ ~ account }
    balance_statement = { "balance" ~ whitespace+ ~ account ~ whitespace+ ~ amount }

transaction = { trx_header ~ newline ~ trx_list }
    trx_header = { trx_state ~ whitespace+ ~ ((trx_payee ~ whitespace+ ~ trx_title) | trx_title) }
    trx_list = { (comment* ~ account_statement ~ newline){2,} }
    trx_state = { ("*" | "!" | "#") }
    trx_title =  { string }
    trx_payee = { string }
    account_statement = {
        whitespace* ~ account ~ (whitespace+ ~ amount?)? ~ comment?
    }

date = @{ year ~ "-" ~ month ~ "-" ~ day_of_month }
    year = { ASCII_NONZERO_DIGIT ~ ASCII_DIGIT{,3} }
    month = { ASCII_DIGIT{2} }
    day_of_month = { ASCII_DIGIT{2} }


account = { account_segment ~ account_suffix }
    account_segment = { UPPERCASE_LETTER ~ (ASCII_ALPHANUMERIC | "-")* }
    account_suffix = @{ (":" ~ account_segment)+ }

amount = { amount_value ~ whitespace+ ~ currency }
    amount_value = @{ "-"? ~ number }
    number = @{ integral ~ fraction? }
    integral = { "0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
    fraction = { "." ~ ASCII_DIGIT+  }
    currency = { UPPERCASE_LETTER+ }


string = ${ "\"" ~ string_content ~ "\"" }
    string_content = @{ char* }
    char = {
        !("\"" | "\\") ~ ANY
        | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
        | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
    }

whitespace = _{ (" " | "\t") }
newline = _{ ("\r\n" | "\r" | "\n") }
