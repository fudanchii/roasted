ledger = _{ SOI ~ ((option | statement | comment) ~ NL*)+ ~ EOI }

option = {
	"option" ~ SPC* ~ string ~ SPC* ~ string
}

comment = _{ SPC* ~ ";" ~ (!"\n" ~ ANY)* }

statement = {
	custom_statement
    | open_statement
    | close_statement
    | commodity_statement
    | price_statement
    | pad_statement
    | balance_statement
    | transaction
}
	custom_statement = { date ~ SPC* ~ "custom" ~ (SPC* ~ string)+ }
	open_statement = { date ~ SPC* ~ "open" ~ SPC* ~ account }
	close_statement = { date ~ SPC* ~ "close" ~ SPC* ~ account }
    commodity_statement = { date ~ SPC* ~ "commodity" ~ SPC* ~ currency }
    price_statement = {
    	date ~ SPC* ~ "price" ~ SPC* ~ currency ~ SPC* ~ amount
    }
    pad_statement = { date ~ SPC* ~ "pad" ~ SPC* ~ account ~ SPC* ~ account }
    balance_statement = {
    	date ~ SPC* ~ "balance" ~ SPC* ~ account ~ SPC* ~ amount
    }

transaction = { date ~ SPC* ~ trx_header ~ trx_list }
	trx_header = { trx_state ~ SPC* ~ string ~ (SPC* ~ string)? }
	trx_list = { (NL ~ comment* ~ account_statement){2,} }
	trx_state = { ("*" | "!") }
	account_statement = {
    	SPC* ~ account ~ (SPC+ ~ amount_with_price?)? ~ comment*
    }
    amount_with_price = { amount ~ (SPC* ~ "@" ~ SPC* ~ price)? }
    price = { amount }

date = @{ year ~ "-" ~ month ~ "-" ~ day_of_month }
	year = { ASCII_NONZERO_DIGIT ~ ASCII_DIGIT{,3} }
	month = { ASCII_DIGIT{2} }
	day_of_month = { ASCII_DIGIT{2} }


account = { account_segment ~ account_suffix }
	account_segment = { UPPERCASE_LETTER ~ (ASCII_ALPHANUMERIC | "-")* }
	account_suffix = @{ (":" ~ account_segment)+ }

amount = { amount_value ~ SPC* ~ currency }
	amount_value = @{ "-"? ~ number }
    number = @{ integral ~ fraction? }
    integral = { "0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
    fraction = { "." ~ ASCII_DIGIT+  }
	currency = { UPPERCASE_LETTER+ }


string = ${ "\"" ~ string_content ~ "\"" }
	string_content = @{ char* }
	char = {
    	!("\"" | "\\") ~ ANY
    	| "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    	| "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
	}

SPC = _{ (" " | "\t") }
NL = _{ ("\r\n" | "\r" | "\n") }
